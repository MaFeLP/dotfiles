# Maintainer: MaFeLP <mafelp@proton.me>
pkgname='custom-scripts'
pkgver='1.9.1'
pkgrel='1'
pkgdesc='Some personal scripts I have written'
arch=('any')
url='https://github.com/mafelp/dotfiles'
license=('GPL3')
depends=(
  'coreutils'
  'github-cli'
  'jq'
  'libnewt'
  'openssh'
  'pacman'
  'python3'
  'rebuild-detector'
  'texlive-core'
  'uutils-coreutils'
  'zsh'
)
optdepends=(
  'buildaur: Update AUR packages with "relax"'
  'flatpak: Update flatpak packages with "relax"'
  'rustup: Update rust toolchain with "relax"'
  'snapd: Update snap packages with "relax"'
)
makedepends=(
  'cargo'
)
source=("files.tar.gz")
sha256sums=("8fd02fbbd7953ee070a2906e8b24c8b79afe920d9b8457417f0b53ab4687621d")

prepare() {
  cd "${srcdir}/files/hlatex"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${srcdir}/files/hlatex"
  cargo build --release --frozen
  gzip "target/mandir/hlatex.1"
}

package() {
  cd "${srcdir}/files"

  for f in "ghdl" "img-convert" "relax" "mirrorlist-update" "working-stage-end.sh";do
    install -Dm755 "$f" "${pkgdir}/usr/bin/$f"
  done

  cd "${srcdir}/files/buildaur/hooks"
  for hook in ./*;do
    install -Dm755 "$hook" "${pkgdir}/etc/buildaur/prehooks/$hook"
  done
  cd "${srcdir}/files/buildaur/patches"
  for patch in $(find . -name '*.patch');do
    install -Dm755 "$patch" "${pkgdir}/etc/buildaur/patches/$patch"
  done

  cd "${srcdir}/files/hlatex/target/"
  install -Dm755 "release/hlatex" "${pkgdir}/usr/bin/hlatex"
  install -Dm755 "mandir/hlatex.1.gz" "${pkgdir}/usr/share/man/man1/hlatex.1.gz"
  install -Dm755 "completions/_hlatex" "${pkgdir}/usr/share/zsh/site-functions/_hlatex"
  install -Dm755 "completions/hlatex.bash" "${pkgdir}/etc/bash_completion.d/hlatex.bash"
  install -Dm755 "completions/hlatex.fish" "${pkgdir}/usr/share/fish/completions/hlatex.fish"

}
